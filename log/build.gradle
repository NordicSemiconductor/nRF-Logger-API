apply plugin: 'com.android.library'

/*
 * I followed this:
 * http://inthecheesefactory.com/blog/how-to-upload-library-to-jcenter-maven-central-as-dependency/en
 * tutorial to publish the library on jcenter and Maven Central repositories.
 * Some changes needed to be done. See below for details.
 *
 * A newer then described version of maven plugin dependency is required (see project's gradle.build).
 */

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    PUBLISH_GROUP_ID = 'no.nordicsemi.android'
    PUBLISH_ARTIFACT_ID = 'log'
    PUBLISH_VERSION = '2.1.1'

    bintrayRepo = 'android'
    bintrayName = 'nrf-logger-api'

    publishedGroupId = PUBLISH_GROUP_ID
    artifact = PUBLISH_ARTIFACT_ID
    libraryVersion = PUBLISH_VERSION
    libraryName = 'nRF Logger API'
    libraryDescription = 'nRF Logger API Library'

    issuesUrl = 'https://github.com/NordicSemiconductor/nRF-Logger-API/issues'
    siteUrl = 'https://github.com/NordicSemiconductor/nRF-Logger-API'
    gitUrl = 'https://github.com/NordicSemiconductor/nRF-Logger-API.git'

    developerId = 'philips77'
    developerName = 'Aleksander Nowakowski'
    developerEmail = 'aleksander.nowakowski@nordicsemi.no'

    licenseName = 'The BSD 3-Clause License'
    licenseUrl = 'http://opensource.org/licenses/BSD-3-Clause'
    allLicenses = ["BSD 3-Clause"]
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 4
        versionName "2.1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'log-proguard-rules.pro'
        }
    }
}

dependencies {
}

afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += configurations.compile
        }
    }
}

// The following script creates a POM file required to publish on Maven Central
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'

// This script creates sources and javadocs. Both are required to publish a library on jcenter and MC.
apply from: 'https://raw.githubusercontent.com/ArthurHub/release-android-library/master/android-release-aar.gradle'

// The following link publishes the library to jcenter. It does not handle the userOrg, so it has been copied and modified below.
//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

// Copied from https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle
version = libraryVersion

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = properties.getProperty("bintray.userOrg")
        desc = libraryDescription
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}
